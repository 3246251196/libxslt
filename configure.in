AC_INIT(libxslt/xslt.c)

VERSION=0.0.1
PACKAGE=libxslt

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE($PACKAGE, $VERSION, no-define)
AM_MAINTAINER_MODE

dnl
dnl Check the environment
dnl

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AC_ARG_PROGRAM
AM_PROG_LIBTOOL

dnl
dnl Math detection
dnl

AC_CHECK_HEADERS(ieeefp.h nan.h math.h fp_class.h float.h)
AC_CHECK_FUNC(isnan, , AC_CHECK_LIB(m, isnan,
  [M_LIBS="-lm"; AC_DEFINE(HAVE_ISNAN)]))

AC_CHECK_FUNC(isinf, , AC_CHECK_LIB(m, isinf,
  [M_LIBS="-lm"; AC_DEFINE(HAVE_ISINF)]))

AC_CHECK_FUNC(pow, , AC_CHECK_LIB(m, pow,
  [M_LIBS="-lm"; AC_DEFINE(HAVE_POW)]))

AC_CHECK_FUNC(floor, , AC_CHECK_LIB(m, pow,
  [M_LIBS="-lm"; AC_DEFINE(HAVE_FLOOR)]))

AC_CHECK_FUNC(fabs, , AC_CHECK_LIB(m, pow,
  [M_LIBS="-lm"; AC_DEFINE(HAVE_FABS)]))


dnl
dnl Debug for DV
dnl
if test "${LOGNAME}" = "veillard" -a "`pwd`" = "/u/veillard/XSLT" ; then
    if test "${with_mem_debug}" = "" ; then
	with_mem_debug="yes"
    fi
    CFLAGS="-Wall -g -pedantic"
fi

AC_ARG_WITH(mem_debug, [  --with-mem-debug        Add the memory debugging module (off)])
if test "$with_mem_debug" = "yes" ; then
    echo Enabling memory debug support
    WITH_MEM_DEBUG=1
else    
    WITH_MEM_DEBUG=0
fi
AC_SUBST(WITH_MEM_DEBUG)

dnl
dnl The following new parameters were added to offer
dnl the ability to specify the location of the libxml
dnl library during linking and compilation.
dnl Mathieu Lacage 30/03/2000
dnl
LIBXML_PREFIX=""
AC_ARG_WITH(libxml-prefix,
        [  --with-libxml-prefix=[PFX]		Specify location of libxml],
	LIBXML_PREFIX=$withval
)
        
AC_ARG_WITH(libxml-include-prefix,
        [  --with-libxml-include-prefix=[PFX]	Specify location of libxml headers],
        LIBXML_CFLAGS="-I$withval"
)

AC_ARG_WITH(libxml-libs-prefix,
        [  --with-libxml-libs-prefix=[PFX]	Specify location of libxml libs],
        LIBXML_LIBS="-L$withval -lxml -lz"
)


dnl No internationalization (yet ?)
dnl 
dnl ALL_LINGUAS="it ko fr de es no ga sv pt ja fi cs"
dnl AM_GNU_GETTEXT
dnl 
dnl AC_LINK_FILES($nls_cv_header_libgt, $nls_cv_header_intl)

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

dnl
dnl find libxml
dnl
XML_CONFIG="xml-config"
AC_MSG_CHECKING(for libxml libraries >= 2.2.12) 
if test "x$LIBXML_PREFIX" != "x"
then
	if ${LIBXML_PREFIX}/bin/xml-config --libs print > /dev/null 2>&1
	then
		XML_CONFIG=${LIBXML_PREFIX}/bin/xml-config
	else
		XML_CONFIG=xml-config
	fi
fi

dnl
dnl test version and init our variables
dnl
if test "x$XML_CONFIG" != "x"
then
	vers=`$XML_CONFIG --version | sed -e 's/libxml //' | awk 'BEGIN { FS = "."; } { printf "%d", ($1 * 1000 + $2) * 1000 + $3;}'`
	if test "$vers" -ge 2002012
	then
		LIBXML_LIBS="`$XML_CONFIG --libs`"
		LIBXML_CFLAGS="`$XML_CONFIG --cflags`"
		AC_MSG_RESULT(found)
	else
		AC_MSG_ERROR(You need at least libxml 2.2.12 for this version of libxml)
	fi
else
	AC_MSG_ERROR(Could not find libxml anywhere.)
fi


AC_SUBST(XML_CONFIG)
AC_SUBST(LIBXML_LIBS)
AC_SUBST(LIBXML_CFLAGS)

XSLT_LIBDIR='-L${libdir}'
XSLT_INCLUDEDIR='-I${includedir}'
EXTRA_LIBS="$LIBXML_LIBS $M_LIBS"
XSLT_LIBS="-lxslt $LIBXML_LIBS $M_LIBS"

AC_SUBST(XSLT_LIBDIR)
AC_SUBST(XSLT_INCLUDEDIR)
AC_SUBST(EXTRA_LIBS)
AC_SUBST(XSLT_LIBS)

AC_OUTPUT([
Makefile
libxslt/Makefile
libxslt/xsltconfig.h
tests/Makefile
tests/REC1/Makefile
tests/REC2/Makefile
xslt-config
libxslt.spec
])
